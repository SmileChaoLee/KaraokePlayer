apply plugin: 'com.android.application'

android {
    signingConfigs {
        release {
            storeFile file('C:\\AndroidStudio\\keystore\\new_key.jks')
            storePassword '86637971'
            keyAlias = 'chaolee1959'
            keyPassword '86637971'
        }
    }
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.smile.karaokeplayer"
        targetSdkVersion 30
        multiDexEnabled true
        versionCode 170
        versionName "1.70"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release

        applicationId "com.smile.karaokeplayer"
        manifestPlaceholders = [google_AdMobAppID: "ca-app-pub-8354869049759576~5549171584"]
    }

    flavorDimensions("player")
    productFlavors {
        exoplayer {
            dimension("player")
            minSdkVersion 16
            manifestPlaceholders = [flavorLegacyExternalStorage: "false"]
        }
        vlcplayer {
            dimension("player")
            minSdkVersion 17
            manifestPlaceholders = [flavorLegacyExternalStorage: "true"]
        }

        videoplayer {
            dimension("player")
            minSdkVersion 17
            applicationId = "com.smile.videoplayer"
            manifestPlaceholders = [flavorLegacyExternalStorage: "true"
                                    , google_AdMobAppID: "ca-app-pub-8354869049759576~5376732060"]
        }

        musicplayer {
            dimension("player")
            minSdkVersion 17
            applicationId "com.smile.musicplayer"
            manifestPlaceholders = [flavorLegacyExternalStorage: "true"
                                    , google_AdMobAppID: "ca-app-pub-8354869049759576~3279561070"]
        }
    }

    sourceSets {
        main {
            java.srcDir("src/main/java")
            resources.srcDir("src/main/rec")
            manifest {
                srcFile "src/main/AndroidManifest.xml"
            }
        }
        exoplayer {
            // java.srcDirs += "src/exoplayer/java"
            // res.srcDirs += "src/exoplayer/res"
            manifest {
                srcFile "src/exoplayer/AndroidManifest.xml"
            }
        }
        vlcplayer {
            // java.srcDirs += "src/common/java"
            // res.srcDirs += "src/vlcplayer/res"
            manifest {
                srcFile "src/vlcplayer/AndroidManifest.xml"
            }
        }

        videoplayer {
            java.srcDirs += "src/vlcplayer/java"
            res.srcDirs += "src/vlcplayer/res"
            manifest {
                srcFile "src/vlcplayer/AndroidManifest.xml"
            }
        }

        musicplayer {
            java.srcDirs += "src/vlcplayer/java"
            res.srcDirs += "src/vlcplayer/res"
            manifest {
                srcFile "src/vlcplayer/AndroidManifest.xml"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.0-alpha02'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.core:core:1.5.0-alpha02'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.annotation:annotation:1.2.0-alpha01'

    implementation 'com.facebook.android:audience-network-sdk:5.11.0'
    implementation 'com.google.firebase:firebase-core:17.5.0'
    implementation 'com.google.firebase:firebase-ads:19.3.0'

    implementation 'com.google.android.gms:play-services-measurement:17.5.0'
    implementation 'com.google.android.gms:play-services-measurement-sdk:17.5.0'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.ext:truth:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    implementation(name: 'smilelibraries-release', ext: 'aar')
    implementation(name: 'NativeTemplatesModels-release', ext: 'aar')

    exoplayerImplementation 'com.google.android.exoplayer:exoplayer:2.11.6'
    exoplayerImplementation 'com.google.android.exoplayer:extension-mediasession:2.11.6'
    exoplayerImplementation 'com.google.android.exoplayer:extension-cast:2.11.6'
    exoplayerImplementation project(':extension-ffmpeg')
    exoplayerImplementation project(':extension-flac')
    exoplayerImplementation project(':extension-opus')
    exoplayerImplementation project(':extension-vp9')
    exoplayerImplementation project(':extension-av1')

    vlcplayerImplementation 'org.videolan.android:libvlc-all:3.3.0-eap15'
    // vlcplayerImplementation 'org.videolan.android:libvlc-all:3.1.12'
    // vlcplayerImplementation 'androidx.mediarouter:mediarouter:1.1.0'
    // vlcplayerImplementation 'com.google.android.gms:play-services-cast-framework:19.0.0'

    videoplayerImplementation 'org.videolan.android:libvlc-all:3.3.0-eap15'

    musicplayerImplementation 'org.videolan.android:libvlc-all:3.3.0-eap15'
}

apply plugin: 'com.google.gms.google-services'
